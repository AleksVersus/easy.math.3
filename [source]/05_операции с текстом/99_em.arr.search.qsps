QSP-Game Находит позицию элемента в массиве, содержащего регулярное выражение, либо возвращает -1
$args[0] - имя массива
$args[1] - регулярное выражение
$args[2] - [first] или [last] - первая или последняя позиция
args[3], args[4] - с какого по какой элемент проводить поиск
!@pp:if(em_arr_search):include
# em.arr.search
local easy_math_temp
$args[9] = $args[9]	& result=-1 & !@ инициализация аргументов и результата по умолчанию
if instr($args[0],'$')<>1: $args[0]='$'+$args[0]	& !@ приводим имя массива к текстовому
$args[2]=$lcase($args[2])	& !@ переводим в нижний регистр
if args[4]<1: args[4] = arrsize($args[0])	& !@ если верхняя позиция не указана, принимается равной размеру массива
copyarr 'easy_math_temp', $args[0]
local pos
if instr($args[2],'[first]')<>0 or $args[2]='':
	!@ режим поиска самого первого элемента, содержащего указанную строку
	pos=arrcomp('easy_math_temp','[\s\S]*'+$args[1]+'[\s\S]*',args[3])
	if pos>args[4]:
		!@ если найденная позиция больше верхнего предела, возвращаем ЛОЖЬ (False)
		result=-1
	else
		!@ в противном случае, возвращаем позицию элемента
		result=pos
	end
elseif instr($args[2],'[last]')<>0:
	loop local i,run=args[3],1 while run:
		pos=arrcomp('easy_math_temp','[\s\S]*'+$args[1]+'[\s\S]*',i)
		if pos<>-1 and pos<=args[4]:
			!@ если позиция найдена, и не превышает верхнюю границу
			result=pos	& !@ запоминаем найденную позицию
			i=result+1	& !@ производим новый поиск со следующего элемента
		else
			run=0
		end
	end
end
--- em.arr.search
 ---------------------------------
!@pp:endif

Функция не использует иных функций библиотеки

$mass[]='привет красное яблоко'
$mass[]='привет оранжевое яблоко'
$mass[]='привет жёлтое яблоко'
$mass[]='привет зелёное яблоко'
$mass[]='привет синее яблоко'
$mass[]='привет голубое яблоко'
$mass[]='привет фиолетовое яблоко'
$mass[]='привет чёрное яблоко'
$mass[]='привет зелёное яблоко'
$mass[]='привет голубое яблоко'
$mass[]='привет фиолетовое яблоко'
$mass[]='привет чёрное яблоко'
@em.arr.search('$mass','зелёное','',4,7)
