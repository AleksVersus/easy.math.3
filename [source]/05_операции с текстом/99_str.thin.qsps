QSP-Game Разрежает строку определённым разделителем
$args[0] - строка текста
args[1] - через какое количество символов вставлять разделитель (по умолчанию 1)
$args[2] - разделитель (по умолчанию - пробел)
$args[3] - управляющая конструкция, см ниже.
!@pp:if(em_str_thin):include
# #str.thin#
$args[9] = $args[9]	& !@ инициализация аргументов
if args[1]<1: args[1]=1	& !@ если разрядность для разрежения не указана, или указана неверно, принимается равным 1
if $args[2] = '': $args[2]=' '	& !@ если разделитель не указан, разделителем служит пробел
$args[3]=$lcase($args[3])	& !@ переводим управляющие конструкции к нижнему регистру
if instr($args[3],'[up]')<>0:
	!@ если разрежение от последнего к первому символу
	:for_up
	if len($args[0])>0:
		!@ цикл выполняется, пока длина строки превышает 0
		$args['t']=$mid($args[0],len($args[0])-(args[1]-1))	& !@ вырезаем из строки последние символы числом args[1]
		$args[0]=$mid($args[0],1,len($args[0])-args[1])	& !@ укорачиваем строку с конца на args[1] символов
		if instr($args[3],'[left]')<>0 or len($args[0])>0:
			!@ если передано управление [left] или длина строки всё ещё больше нуля
			$args['res']=$args[2]+$args['t']+$args['res']	& !@ добавляем к результату слева разделитель и вырезанные символы
		else
			!@ в противном случае
			$args['res']=$args['t']+$args['res']	& !@ добавляем только символы
		end
		jump 'for_up'
	end
	if instr($args[3],'[right]')<>0: $args['res']+=$args[2]	& !@ если [right], добавляем разделитель справа к результату
else
	:for_down
	if len($args[0])>0:
		!@ цикл выполняется, пока длина строки превышает 0
		$args['t']=$mid($args[0],1,args[1])	& !@ вырезаем из строки первые символы числом args[1]
		$args[0]=$mid($args[0],args[1]+1)	& !@ строку укорачиваем на args[1] символов с начала
		if instr($args[3],'[right]')<>0 or len($args[0])>0:
			!@ если передано управление [right] или длина строки всё ещё больше нуля
			$args['res']+=$args['t']+$args[2]	& !@ добавляем к результату справа разделитель и вырезанные символы
		else
			!@ в противном случае
			$args['res']+=$args['t']	& !@ только символы
		end
		jump 'for_down'
	end
	if instr($args[3],'[left]')<>0: $args['res']=$args[2]+$args['res']	& !@ если [left], добавляем разделитель справа к результату
end
$result = $args['res']	& !@ возвращаем результат
--- #str.thin# ---------------------------------
!@pp:endif

Функция не использует иных функций библиотеки

Управление:for
[up] - разрежает текст от последнего символа к первому
[right] - добавляет разделитель в конце результирующей строки
[left] - добавляет разделитель в начале результирующей строки