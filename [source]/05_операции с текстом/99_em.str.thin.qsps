QSP-Game Разрежает строку определённым разделителем
$args[0] - строка текста
args[1] - через какое количество символов вставлять разделитель (по умолчанию 1)
$args[2] - разделитель (по умолчанию - пробел)
$args[3] - управляющая конструкция, см ниже.
!@pp:if(em_str_thin):include
# em.str.thin
if args[1]<1: args[1]=1	& !@ если разрядность для разрежения не указана, или указана неверно, принимается равным 1
if $args[2] = '': $args[2]=' '	& !@ если разделитель не указан, разделителем служит пробел
$args[3]=$lcase($args[3])	& !@ переводим управляющие конструкции к нижнему регистру
local $string,spacing,$delimiter,$commands=$args[0],args[1],$args[2],$args[3]	& !@ инициализация локальных переменных
local $res
if instr($commands,'[up]')<>0:
	!@ если разрежение от последнего к первому символу
	loop local $t='' while len($string)>0 step:
		!@ цикл выполняется, пока длина строки превышает 0
		$t=$mid($string,len($string)-(spacing-1))	& !@ вырезаем из строки последние символы числом spacing
		$string=$mid($string,1,len($string)-spacing)	& !@ укорачиваем строку с конца на spacing символов
		if instr($commands,'[left]')<>0 or len($string)>0:
			!@ если передано управление [left] или длина строки всё ещё больше нуля
			$res=$delimiter+$t+$res	& !@ добавляем к результату слева разделитель и вырезанные символы
		else
			!@ в противном случае
			$res=$t+$res	& !@ добавляем только символы
		end
	end
	if instr($commands,'[right]')<>0: $res+=$delimiter	& !@ если [right], добавляем разделитель справа к результату
else
	loop local $t='' while len($string)>0 step:
		!@ цикл выполняется, пока длина строки превышает 0
		$t=$mid($string,1,spacing)	& !@ вырезаем из строки первые символы числом spacing
		$string=$mid($string,spacing+1)	& !@ строку укорачиваем на spacing символов с начала
		if instr($commands,'[right]')<>0 or len($string)>0:
			!@ если передано управление [right] или длина строки всё ещё больше нуля
			$res+=$t+$delimiter	& !@ добавляем к результату справа разделитель и вырезанные символы
		else
			!@ в противном случае
			$res+=$t	& !@ только символы
		end
	end
	if instr($commands,'[left]')<>0: $res=$delimiter+$res	& !@ если [left], добавляем разделитель слева к результату
end
$result = $res	& !@ возвращаем результат
--- em.str.thin ---------------------------------
!@pp:endif

Функция не использует иных функций библиотеки

Управление:for
[up] - разрежает текст от последнего символа к первому
[right] - добавляет разделитель в конце результирующей строки
[left] - добавляет разделитель в начале результирующей строки

@em.str.thin('профессура')	& !@ "п р о ф е с с у р а"
@em.str.thin('профессура',1,'|')	& !@ "п|р|о|ф|е|с|с|у|р|а"
@em.str.thin('профессура',1,'|','[right]')			& !@  "п|р|о|ф|е|с|с|у|р|а|"
@em.str.thin('профессура',1,'|','[left]')			& !@ "|п|р|о|ф|е|с|с|у|р|а"
@em.str.thin('профессура',1,'|','[right][left]')	& !@ "|п|р|о|ф|е|с|с|у|р|а|"
@em.str.thin('профессура',3,'|')	& !@ "про|фес|сур|а"
@em.str.thin('профессура',3,'|','[up]')	& !@ "п|роф|есс|ура"
@em.str.thin('профессура',3,'|','[left]')	& !@ "|про|фес|сур|а"
@em.str.thin('профессура',2,'|','[right]')	& !@ "пр|оф|ес|су|ра|"
@em.str.thin('профессура',1,'||','[left] [right]')	& !@ "||п||р||о||ф||е||с||с||у||р||а||"
