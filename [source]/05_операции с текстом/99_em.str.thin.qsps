QSP-Game Разрежает строку определённым разделителем

!@ $0 - строка текста
!@ #1 - через какое количество символов вставлять разделитель (по умолчанию 1)
!@ $2 - разделитель (по умолчанию - пробел)
!@ $3 - управляющая конструкция, см ниже.
!@pp:if(em_str_thin):include
# em.str.thin
local $string_, spacing_, $delimiter_, $mode_ = $args[0], args[1], $args[2], $args[3]	& !@ инициализация локальных переменных
if spacing_ < 1: spacing_ = 1	& !@ если разрядность для разрежения не указана, или указана неверно, принимается равным 1
if $delimiter_ = '': $delimiter_ = ' '	& !@ если разделитель не указан, разделителем служит пробел
$mode_ = $lcase($mode_)	& !@ переводим управляющие конструкции к нижнему регистру
local $res_, len_, mod_
len_ = len($string_)
mod_ = len_ mod spacing_
if instr($mode_,'[up]') <> 0:
	if mod_ > 0: $string_ = $mid($string_, 1, spacing_ - mod_) + $string_
	scanstr '$res_', $string_, '[\s\S]{<<spacing_>>}'
	if mod_ > 0: $res_[0] = $mid($res_[0], spacing_)
else:
	if mod_ > 0: $string_ += $mid($string_, 1, spacing_ - mod_)
	scanstr '$res_', $string_, '[\s\S]{<<spacing_>>}'
	if mod_ > 0: $res_[arrsize('$res_')-1] = $mid($res_[], 1, mod_)
end
loop while arrsize('$res_'):
	if len($result) or instr($mode_, '[left]'):
		$result += $delimiter_ + $res_[0]
	else:
		$result += $res_[0]
	end
	killvar '$res_', 0
end
if instr($mode_,'[right]')<>0: $result += $delimiter_	& !@ если [right], добавляем разделитель справа к результату
--- em.str.thin ---------------------------------
!@pp:endif

Функция не использует иных функций библиотеки

Управление:for
[up] - разрежает текст от последнего символа к первому
[right] - добавляет разделитель в конце результирующей строки
[left] - добавляет разделитель в начале результирующей строки

@em.str.thin('профессура')	& !@ "п р о ф е с с у р а"
@em.str.thin('профессура',1,'|')	& !@ "п|р|о|ф|е|с|с|у|р|а"
@em.str.thin('профессура',1,'|','[right]')			& !@  "п|р|о|ф|е|с|с|у|р|а|"
@em.str.thin('профессура',1,'|','[left]')			& !@ "|п|р|о|ф|е|с|с|у|р|а"
@em.str.thin('профессура',1,'|','[right][left]')	& !@ "|п|р|о|ф|е|с|с|у|р|а|"
@em.str.thin('профессура',3,'|')	& !@ "про|фес|сур|а"
@em.str.thin('профессура',3,'|','[up]')	& !@ "п|роф|есс|ура"
@em.str.thin('профессура',3,'|','[left]')	& !@ "|про|фес|сур|а"
@em.str.thin('профессура',2,'|','[right]')	& !@ "пр|оф|ес|су|ра|"
@em.str.thin('профессура',1,'||','[left] [right]')	& !@ "||п||р||о||ф||е||с||с||у||р||а||"
010 123 456 789