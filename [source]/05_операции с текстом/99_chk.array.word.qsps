QSP-Game Находит позицию элемента в массиве, содержащего регулярное выражение, либо возвращает -1
$args[0] - имя массива
$args[1] - регулярное выражение
$args[2] - [first] или [last] - первая или последняя позиция
args[3], args[4] - с какого по какой элемент проводить поиск
!@pp:if(em_chk_array_word):include
# #chk.array.word#
$args[9] = $args[9]	& result=-1 & !@ инициализация аргументов и результата по умолчанию
if instr($args[0],'$')<>1: $args[0]='$'+$args[0]	& !@ приводим имя массива к текстовому
$args[2]=$lcase($args[2])	& !@ переводим в нижний регистр
if args[4]<1: args[4] = arrsize($args[0])	& !@ если верхняя позиция не указана, принимается равной размеру массива
if instr($args[2],'[first]')<>0 or $args[2]='':
	!@ режим поиска самого первого элемента, содержащего указанную строку
	if instr($AVS_PLAYERTYPE,'[QN]')<>0:
		!@ если включен режим для Quest Navigator
		args['b']=arrcomp($args[0],'[\s\S]*'+$args[1]+'[\s\S]*',args[3])
	else
		!@ если включен режим для классического плеера
		args['b']=arrcomp(args[3],$args[0],'[\s\S]*'+$args[1]+'[\s\S]*')
	end
	if args['b']>args[4]:
		!@ если найденная позиция больше верхнего предела, возвращаем ЛОЖЬ (False)
		result=-1
	else
		!@ в противном случае, возвращаем позицию элемента
		result=args['b']
	end
elseif instr($args[2],'[last]')<>0:
	args['i']=args[3]	& !@ выставляем счётчик по числу нижней границы
	:for
	if instr($AVS_PLAYERTYPE,'[QN]')<>0:
		args['b']=arrcomp($args[0],'[\s\S]*'+$args[1]+'[\s\S]*',args['i'])
	else
		args['b']=arrcomp(args['i'],$args[0],'[\s\S]*'+$args[1]+'[\s\S]*')
	end
	if args['b']<>-1 and args['b']<=args[4]:
		!@ если позиция найдена, и не превышает верхнюю границу
		result=args['b']	& !@ запоминаем найденную позицию
		args['i']=result+1	& !@ производим новый поиск со следующего элемента
		jump 'for'
	end
end
--- #chk.array.word# ---------------------------------
!@pp:endif

Функция не использует иных функций библиотеки