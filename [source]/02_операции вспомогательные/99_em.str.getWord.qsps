QSP-Game Функция из строки вида aaa|bbb|ccc|...|yyy|zzz получает подстроку, стоящую в указанной позиции. Подстроки помещаются между разделителями. По умолчанию разделителем является вертикальная черта.
$[0] - строка
[1] - позиция, из которой нужно получить подстроку.
$[2] - разделитель. По умолчанию в качестве разделителя используется вертикальная черта.
!@pp:if(em_str_getWord):include
# em.str.getWord
local $src_string_, pos_, $delimiter_ = $args[0], args[1], $args[2]
local $cut_string_
if $delimiter_ = '': $delimiter_='|'	& !@ по умолчанию разделителем выступает вертикальная черта
$src_string_ = $src_string_ + $delimiter_	& !@ для упрощения вычислений добавляем разделитель к финалу
loop local instr_ while len($src_string_)>0 and pos_>0 step pos_-=1:
	!@ вычисления продолжаются пока не разобрана вся строка, либо пока счётчик не будет исчерпан
	instr_ = instr($src_string_, $delimiter_)
	if instr_ > 0:
		!@ если разделитель в строке найден
		$cut_string_ = $mid($src_string_, 1, instr_ + len($delimiter_) - 1)	& !@ вырезаем до разделителя
		$src_string_ = $mid($src_string_, instr_ + len($delimiter_))	& !@ строкой становится хвост после разделителя
	end
end
$result=$replace($cut_string_, $delimiter_)
--- em.str.getWord ---------------------------------
!@pp:endif

Функция не использует иных функций библиотеки

Январь|Февраль|Март|Апрель|Май|Июнь|Июль|Август|Сентябрь|Октябрь|Ноябрь|Декабрь
12345678901234567890123456789012345678901234567890123456789012345678901234567890
        10        20        30        40        50        60        70

$text="Январь|Февраль|Март|Апрель|Май|Июнь|Июль|Август|Сентябрь|Октябрь|Ноябрь|Декабрь"

@em.str.getWord($text,2)	& !@ "Февраль"
@em.str.getWord($text,5)	& !@ "Май"
@em.str.getWord($text,8)	& !@ "Август"
@em.str.getWord($text,11)	& !@ "Ноябрь"
@em.str.getWord($text,14)	& !@ "Декабрь"
@em.str.getWord($text,2,"ь")	& !@ "|Феврал"
@em.str.getWord($text,5,"р")	& !@ "ель|Май|Июнь|Июль|Август|Сентяб"
@em.str.getWord($text,8,"е")	& !@ "кабрь"
@em.str.getWord($text,11,"я")	& !@ "брь|Декабрь"
@em.str.getWord($text,14,"а")	& !@ "брь"
@em.str.getWord($text,3,"брь")	& !@ "|Ноя"

