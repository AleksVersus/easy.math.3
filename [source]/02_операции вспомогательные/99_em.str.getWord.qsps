QSP-Game Функция из строки вида aaa|bbb|ccc|...|yyy|zzz получает подстроку, стоящую в указанной позиции. Подстроки помещаются между разделителями. По умолчанию разделителем является вертикальная черта.
$args[0] - строка
args[1] - позиция, из которой нужно получить подстроку.
$args[2] - разделитель. По умолчанию в качестве разделителя используется вертикальная черта.
!@pp:if(em_str_getWord):include
# em.str.getWord
local $cut_string
if $args[2] = '': $args[2]='|'	& !@ по умолчанию разделителем выступает вертикальная черта
$args[0] = $args[0]+$args[2]	& !@ для упрощения вычислений добавляем разделитель к финалу
loop local delimiter while len($args[0])>0 and args[1]>0 step args[1]-=1:
	!@ вычисления продолжаются пока не разобрана вся строка, либо пока счётчик не будет исчерпан
	delimiter=instr($args[0],$args[2])
	if delimiter>0:
		!@ если разделитель в строке найден
		$cut_string = $mid($args[0],1,delimiter+len($args[2])-1)	& !@ вырезаем до разделителя
		$args[0] = $mid($args[0],delimiter+len($args[2]))	& !@ строкой становится хвост после разделителя
	end
end
$result=$replace($cut_string,$args[2])
--- em.str.getWord ---------------------------------
!@pp:endif

Функция не использует иных функций библиотеки

Январь|Февраль|Март|Апрель|Май|Июнь|Июль|Август|Сентябрь|Октябрь|Ноябрь|Декабрь
12345678901234567890123456789012345678901234567890123456789012345678901234567890
        10        20        30        40        50        60        70

$text="Январь|Февраль|Март|Апрель|Май|Июнь|Июль|Август|Сентябрь|Октябрь|Ноябрь|Декабрь"

@em.str.getWord($text,2)	& !@ "Февраль"
@em.str.getWord($text,5)	& !@ "Май"
@em.str.getWord($text,8)	& !@ "Август"
@em.str.getWord($text,11)	& !@ "Ноябрь"
@em.str.getWord($text,14)	& !@ "Декабрь"
@em.str.getWord($text,2,"ь")	& !@ "|Феврал"
@em.str.getWord($text,5,"р")	& !@ "ель|Май|Июнь|Июль|Август|Сентяб"
@em.str.getWord($text,8,"е")	& !@ "кабрь"
@em.str.getWord($text,11,"я")	& !@ "брь|Декабрь"
@em.str.getWord($text,14,"а")	& !@ "брь"
@em.str.getWord($text,3,"брь")	& !@ "|Ноя"

