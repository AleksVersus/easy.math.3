QSP-Game Функция из строки вида aaa|bbb|ccc|...|yyy|zzz получает подстроку, стоящую в указанной позиции.

!@ $0 - строка
!@ #1 - позиция, из которой нужно получить подстроку.
!@ $2 - разделитель. По умолчанию в качестве разделителя используется вертикальная черта |.
!@ $r - подстрока в указанной позиции
!@pp:if(em_str_get_word):include
# em.str.get_word
local $src_string_, pos_, $delimiter_ = $args[0], args[1], $args[2]
local $regexp_, $matches_
if $delimiter_ = '': $delimiter_='|'	& !@ по умолчанию разделителем выступает вертикальная черта
$src_string_ += $delimiter_	& !@ для упрощения вычислений добавляем разделитель к финалу
$regexp_ = '([\s\S]*?)' + @em.str.in_regexp($delimiter_)
scanstr '$matches_', $src_string_, $regexp_
if args[1] > arrsize('$matches_'): args[1] = arrsize('$matches_')
$result = $matches_[args[1]-1]
$result = $strfind($result, $regexp_+'$', 1)
--- em.str.get_word ---------------------------------
!@pp:endif

Функция не использует иных функций библиотеки

Январь|Февраль|Март|Апрель|Май|Июнь|Июль|Август|Сентябрь|Октябрь|Ноябрь|Декабрь
12345678901234567890123456789012345678901234567890123456789012345678901234567890
        10        20        30        40        50        60        70

$text="Январь|Февраль|Март|Апрель|Май|Июнь|Июль|Август|Сентябрь|Октябрь|Ноябрь|Декабрь"

@em.str.get_word($text,2)	& !@ "Февраль"
@em.str.get_word($text,5)	& !@ "Май"
@em.str.get_word($text,8)	& !@ "Август"
@em.str.get_word($text,11)	& !@ "Ноябрь"
@em.str.get_word($text,14)	& !@ "Декабрь"
@em.str.get_word($text,2,"ь")	& !@ "|Феврал"
@em.str.get_word($text,5,"р")	& !@ "ель|Май|Июнь|Июль|Август|Сентяб"
@em.str.get_word($text,8,"е")	& !@ "кабрь"
@em.str.get_word($text,11,"я")	& !@ "брь|Декабрь"
@em.str.get_word($text,14,"а")	& !@ "брь"
@em.str.get_word($text,3,"брь")	& !@ "|Ноя"

