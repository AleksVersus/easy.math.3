QSP-Game Получает содержимое одиночного тега из строки.

!@ $0 - текст, содержащий тег.
!@ $1 - корень тега.
!@ $r - содержимое тега
!@pp:if(em_tag_getnum):include
# em.tag.getNum
local it_hex, $res_num = 0, ''	& !@ инициализация локальных переменных
!@ если указан ключ /h - в результат можно выводить только шестнадцатеричное число
if $mid($args[1],len($args[1])-1)='/h': $args[1]=$replace($args[1],'/h') & it_hex=1
if $strfind($args[0],'\['+$args[1]+':'+'([^][]*?)'+'\]')<>'':
	!@ [tag:any symbols]
	$res_num = $strfind($args[0],'\['+$args[1]+':'+'([^][]+?)'+'\]',1)
elseif $strfind($args[0],'<'+$args[1]+':'+'([^><]*?)'+'>')<>'':
	!@ <tag:any symbols>
	$res_num = $strfind($args[0],'<'+$args[1]+':'+'([^><]+?)'+'>',1)
elseif $strfind($args[0],'\{'+$args[1]+':'+'([^}{]*?)'+'\}')<>'':
	!@ {tag:any symbols}
	$res_num = $strfind($args[0],'\{'+$args[1]+':'+'([^}{]+?)'+'\}',1)
elseif $strfind($args[0],'\('+$args[1]+':'+'([^)(]*?)'+'\)')<>'':
	!@ (tag:any symbols)
	$res_num = $strfind($args[0],'\('+$args[1]+':'+'([^)(]+?)'+'\)',1)
elseif $strfind($args[0],$args[1]+'=("|'+"')([\s\S]*?)\1")<>'':
	!@ tag="any symbols"
	!@ tag='any symbols'
	$res_num = $strfind($args[0],$args[1]+'=("|'+"')([\s\S]*?)\1",2)
elseif $strfind($args[0],$args[1]+'(:|=#)[\S]+')<>'':
	!@ tag:non_space_symbols
	!@ tag=#non_space_symbols
	$res_num = $strfind($args[0],$args[1]+'(:|=#)([\S]+)',2)
end
if it_hex and $strfind($res_num,'^([0-9A-Fa-f\-]+)([\S]*)$')='':
	!@ если число не шестнадцатеричное, но был установлен ключ, ничего не возвращаем
	killvar '$result'
	exit
elseif it_hex:
	!@ если ключ был установлен и число шестнадцатеричное, получаем это число
	$res_num = $strfind($res_num,'^([0-9A-Fa-f\-]+)([\S]*)$',1)
end
!@ результат возвращаем только в текстовом виде
$result=$res_num
--- em.tag.getNum ---------------------------------
!@pp:endif

Функция не использует иных функций библиотеки.

$text="[count:текст с пробелом] <cord:68> log:непробельные_символы color=#ff8899 tag='многа букав'"

@em.tag.getNum($text,'count')	& !@ "текст с пробелом"
@em.tag.getNum($text,'cord')	& !@ '68'
@em.tag.getNum($text,'log')	& !@ "непробельные_символы"
@em.tag.getNum($text,'color')	& !@ "ff8899"
@em.tag.getNum($text,'tag')	& !@ "многа букав"
@em.tag.getNum($text,'tag/h')	& !@ не выведет ничего
@em.tag.getNum($text,'color/h')	& !@ "ff8899"
