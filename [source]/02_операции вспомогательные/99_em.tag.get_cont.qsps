QSP-Game Получает содержимое двойного тега из строки.

!@ $0 - текст, содержащий тег.
!@ $1 - корень тега.
!@ $r - содержимое тега
!@pp:if(em_tag_get_cont):include
# em.tag.get_cont
if ($args[1]='<!--!>' or $args[1]='<!>') and $strfind($args[0],'<!--([\s\S]+?)-->')<>'':
	!@ содержимое комментария
	$result = $strfind($args[0],'<!--([\s\S]+?)-->',1)
elseif $strfind($args[0],'\['+$args[1]+':([\s\S]+?):'+$args[1]+'\]')<>'':
	!@ [tag:any symbols:tag]
	$result = $strfind($args[0],'\['+$args[1]+':([\s\S]+?):'+$args[1]+'\]',1)
elseif $strfind($args[0],'\('+$args[1]+':([\s\S]+?):'+$args[1]+'\)')<>'':
	!@ (tag:any symbols:tag)
	$result = $strfind($args[0],'\('+$args[1]+':([\s\S]+?):'+$args[1]+'\)',1)
elseif $strfind($args[0],'\['+$args[1]+'\]([\s\S]+?)\[\/'+$args[1]+'\]')<>'':
	!@ [tag]any symbols[/tag]
	$result = $strfind($args[0],'\['+$args[1]+'\]([\s\S]+?)\[\/'+$args[1]+'\]?',1)
elseif $strfind($args[0],'<'+$args[1]+':([\s\S]+?):'+$args[1]+'>')<>'':
	!@ <tag:any symbols:tag>
	$result = $strfind($args[0],'<'+$args[1]+':([\s\S]+?):'+$args[1]+'>',1)
elseif $strfind($args[0],'<'+$args[1]+'>([\s\S]+?)<\/'+$args[1]+'>')<>'':
	!@ <tag>any symbols</tag>
	$result = $strfind($args[0],'<'+$args[1]+'>([\s\S]+?)<\/'+$args[1]+'>',1)
elseif $strfind($args[0],$args[1]+':([\s\S]+?):'+$args[1])<>'':
	!@ tag:any symbols:tag
	$result = $strfind($args[0],$args[1]+':([\s\S]+?):'+$args[1],1)
end
--- em.tag.get_cont ---------------------------------
!@pp:endif

Функция не использует иных функций библиотеки.

$text="[count:текст с пробелом:count] <cord:странный и непонятный текст:cord> log:п р о б е л ь н ы е и непробельные символы:log <color>ff8899</color> [tag]многа букав[/tag]"

@em.tag.get_cont($text,'count')	& !@ "текст с пробелом"
@em.tag.get_cont($text,'cord')	& !@ "странный и непонятный текст"
@em.tag.get_cont($text,'log')	& !@ "п р о б е л ь н ы е и непробельные символы"
@em.tag.get_cont($text,'color')	& !@ "ff8899"
@em.tag.get_cont($text,'tag')	& !@ "многа букав"
