QSP-Game Функция сложения. Может суммировать целые и дробные числа любого знака любой длины (ограничение на значение текстовых переменных для QSP 5.7.0 около 2Гб).
$args[0] ... $args[8] - девять слагаемых. Могут быть переданы в числовом виде (для QSP 5.7.0 в пределах [-2147483648 ... 2147483647]).
$result - всегда текстовое значение.
!@pp:if(em_addition):include
# #+#
$args[9]=$args[9]	& !@ инициализация аргументов
!@ инициализация чисел
args['i']=0
args['b']=-1
:initial
if args['i']<9:
	if $args[args['i']]='' and args[args['i']]<>0:
		!@ если передана не строка, а число, переводим число в строку
		$args[args['i']]=$str(args[args['i']])
	end
	if $args[args['i']]<>'' and $strfind($args[args['i']],'^0+(\.0+)?$')='':
		args['b']+=1	& !@ счётчик найденных чисел
		!@ если передана строка, и дробное число в ней не равно нулю
		if instr($args[args['i']],'-')=1:
			!@ если в чииле есть знак минус, помечаем число минусом
			$args["+<<args['b']>>"]='-'
			$args[args['i']]=$mid($args[args['i']],2)
		else
			$args["+<<args['b']>>"]=''
			$args[args['i']]=$args[args['i']]
		end
		!@ если в числе есть точка, разделяем число на целую и дробную часть
		if instr($args[args['i']],'.')<>0:
			$args["int_<<args['b']>>"]=$mid($args[args['i']],1,instr($args[args['i']],'.')-1)
			$args["float_<<args['b']>>"]=$mid($args[args['i']],instr($args[args['i']],'.')+1)
		else
			$args["int_<<args['b']>>"]=$args[args['i']]
			$args["float_<<args['b']>>"]='0'
		end
		!@ если длина числа больше максимальной, записываем в максимальные эту длину
		if len($args["int_<<args['b']>>"])>args['int_size']: args['int_size']=len($args["int_<<args['b']>>"])
		if len($args["float_<<args['b']>>"])>args['float_size']: args['float_size']=len($args["float_<<args['b']>>"])
	end
	args['i']+=1
	jump 'initial'
end
!@ увеличиваем число разрядов в числе, чтобы избежать различных эксцессов
args['int_size']+=2
args['float_size']+=2
!@ следующий цикл приводит число к целому для сложения с переполнением
args['i']=0
$args['dev']=$func('#zero#',args['int_size'],args['float_size'],'9')
:к_целому
if args['i']<=args['b']:
	$args["number_<<args['i']>>"]=$func('#zero#',args['int_size']-len($args["int_<<args['i']>>"]))+$args["int_<<args['i']>>"]+$args["float_<<args['i']>>"]+$func('#zero#',args['float_size']-len($args["float_<<args['i']>>"]))
	if $args["+<<args['i']>>"]='-':
		$args["number_<<args['i']>>"]=$func('#dev#',$args['dev'],$args["number_<<args['i']>>"])
	end
	args['i']+=1
	jump 'к_целому'
end
!@ следующий цикл - суммирование чисел
args['i']=0
$args['sum']=$func('#zero#',args['int_size'],args['float_size'])
:for
if args['i']<=args['b']:
	$args['sum']=$func('#add#',$args['sum'],$args["number_<<args['i']>>"])
	args['i']+=1
	jump 'for'
end
if instr($args['sum'],'9')=1:
	$args['result']=$func('#dev#',$args['dev'],$args['sum'])
	$args['+']='-'
else
	$args['result']=$args['sum']
	$args['+']=''
end
$args['result']=$args['+']+$mid($args['result'],1,args['int_size'])+"."+$mid($args['result'],args['int_size']+1)
$result=$func('#dz#',$args['result'])
--- #+# ---------------------------------
!@pp:endif

Функция использует функции add, dev, zero, dz

!@pp:if(em_addition!=True):nopp
!@pp:var(em_dz)
!@pp:var(em_zstr)
!@pp:var(em_add)
!@pp:var(em_dev)
!@pp:endif