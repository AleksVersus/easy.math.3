QSP-Game Переводит рациональное число в целое. (фактически умножает рациональное число на круглое число, с округлением-отсечением дробной части)
$args[0] - рациональное число
args[1] - точность перевода 10 - один знак после запятой, 100 - два знака, и т.д, по умолчанию до последнего знака после запятой
result - целое число
!@pp:if(em_undiv):include
# em.undiv
local $zero_num, dot_in_str, $end_part, $start_part	& !@ инициализация локальных переменных
!@ приводим args[1] к круглому значению
if (args[1]<>0) and (args[1] mod 10 <> 0):
	!@ ------------ этот отрезок кода можно заменить на функцию em.digs -------------
		if args[1]<0: args[1]=-args[1]	& !@ модуль числа для удобства вычислений
		local base_10=1 
		loop while no (args[1]/base_10<10):
			base_10*=10
		end
		args[1]=base_10
	!@ ------------ этот отрезок кода можно заменить на функцию em.digs ------------
end
if isnum($args[0])<>0 and args[1]<>0:
	!@ если строка не содержит точку и args[1] задан, просто умножаем число на args[1]
	result=val($args[0])*args[1]
	exit
elseif isnum($args[0])<>0 and args[1]=0:
	!@ если args[1] не задан и точки в строке нет, возвращаем само число
	result=val($args[0])
else
	!@ получаем только нули из args[1] в виде строки
	$zero_num=$mid($str(args[1]),2)
	!@ если вернулась пустая строка, просто удаляем точку из числа
	if $zero_num='': result=val($replace($args[0],'.')) & exit
	dot_in_str=instr($args[0],'.')				& !@ получаем позицию точки в строке
	$end_part=$mid($args[0],dot_in_str+1)		& !@ получаем часть после точки
	$start_part=$mid($args[0],1,dot_in_str-1)	& !@ получаем часть до точки
	if len($end_part)>=len($zero_num):
		!@ если длина конечной части числа равна, или превышает длину строки нулей
		!@ к начальной части добавляем часть конечной, равной длине строки нулей
		result=val($start_part+$mid($end_part,1,len($zero_num)))
	else
		!@ если длина конечной части меньше длины нулей, начальную и конечную часть склеиваем и добавляем несколько нулей
		result=val($start_part+$end_part+$mid($zero_num,1,len($zero_num)-len($end_part)))
	end
end
--- em.undiv ---------------------------------
!@pp:endif

Функция не использует иных функций библиотеки

@em.undiv('1',1000)	& !@ 1000
@em.undiv('-1',1000)	& !@ -1000
@em.undiv('0.123',100)	& !@ 12
@em.undiv('0.123',10)	& !@ 1
@em.undiv('0.123')	& !@ 123
@em.undiv('1.23')	& !@ 123
