QSP-Game Delete Zero - одна из основных функций, позволяющая упразднять несущественные нули в конце натурального числа, а так же все несущественные нули в начале.
$args[0] - обрабатываемое число, только в текстовом виде!!!
args[1] - разряд до которого требуется удалить нули. 1 - до целых, 10 - до десятых, 100 - до сотых и т.д, по умолчанию 1.
$result - текстовое значение вида 000.000
!@pp:if(em_dz):include
# em.dz
local $minus, dot, $start_part, $end_part, zero_num, $no_zeros	& !@ инициализируем аргументы
if instr($args[0],'-')=1:
	$args[0]=$mid($args[0],2)
	$minus='-'
end
if args[1]<1:
	args[1]=1
else:
	!@ ------------ этот отрезок кода можно заменить на функцию em.digs -------------
		args[1] = dyneval({
			loop result = 1 while no (args[0]/result < 10): result *= 10
		}, args[1])
	!@ ------------ этот отрезок кода можно заменить на функцию em.digs ------------
end

if no instr($args[0], '.'):
	!@ если точки нет, добавляем
	$args[0] += '.0'
end
dot=instr($args[0],'.')	& !@ получаем позицию точки в строке
$start_part=$mid($args[0],1,dot-1)	& !@ часть числа до точки
$end_part=$mid($args[0],dot+1)	& !@ часть числа после точки
zero_num=len($str(args[1]))-1	& !@ число нулей в аргументе указывающем разрядность дробной части
!@ убираем лишние нули в дробной части
$no_zeros=$strfind($end_part,'(\d*?)(0*)$',1)	& !@ получаем число до нулей
if $no_zeros='':
	$end_part=''
elseif len($no_zeros)>=zero_num:
	!@ если длина строки без нулей больше числа разрядов, или равна ему
	$end_part=$no_zeros	& !@ дробная часть приводится к строке без нулей
else
	!@ если число разрядов больше строки без нулей
	$end_part=$mid($end_part,1,zero_num)
end
!@ убираем лишние нули в целой части
$start_part=$strfind($start_part,'(0*)(\d*)$',2)
if $start_part='': $start_part='0'	& !@ если в целой части были только нули, оставляем один ноль
! @ если дробная часть не отсеклась, возвращаем число с точкой, иначе - без
if $end_part<>'':
	$result=$minus+$start_part+'.'+$end_part
elseif $start_part<>0:
	$result=$minus+$start_part
else
	$result='0'
end
--- em.dz ---------------------------------
!@pp:endif

Функция не использует иных функций библиотеки

@em.dz('64',100)	& !@ '64'
@em.dz('64.0080000',1)	& !@ '64.008'
@em.dz('64.0080000',100)	& !@ '64.008'
@em.dz('64.0080000',10000)	& !@ '64.0080'
