QSP-Game Delete Zero - одна из основных функций, позволяющая упразднять несущественные нули в конце натурального числа, а так же все несущественные нули в начале.
$args[0] - обрабатываемое число, только в текстовом виде!!!
args[1] - разряд до которого требуется удалить нули. 1 - до целых, 10 - до десятых, 100 - до сотых и т.д, по умолчанию 1.
$result - текстовое значение вида 000.000
!@pp:if(em_dz):include
# #dz#
$args[9] = $args[9]	& !@ инициализируем аргументы
if instr($args[0],'-')=1:
	$args[0]=$mid($args[0],2)
	$args['+']='-'
end
if args[1]<1:
	args[1]=1
else:
	!@ ------------ этот отрезок кода можно заменить на функцию em_raz -------------
		args['10']=1	& !@ то самое основание
		:loop_10
		if no (args[1]/args['10']<10):
			args['10']=args['10']*10
			jump 'loop_10'
		end
	!@ ------------ этот отрезок кода можно заменить на функцию em_raz ------------
end
args['dot']=instr($args[0],'.')				& !@ получаем позицию точки в строке
if args['dot']=0:
	!@ если точки нет, добавляем
	$args[0]+='.0'
	args['dot']=instr($args[0],'.')
end
$args['start']=$mid($args[0],1,args['dot']-1)	& !@ часть числа до точки
$args['end']=$mid($args[0],args['dot']+1)	& !@ часть числа после точки
args['numofzero']=len($str(args[1]))-1	& !@ число нулей в аргументе указывающем разрядность дробной части
!@ убираем лишние нули в дробной части
$args['nozero']=$strfind($args['end'],'(\d*?)(0*)$',1)	& !@ получаем число до нулей
$args['zero']=$strfind($args['end'],'(\d*?)(0*)$',2)	& !@ получаем нули в конце дробной части
if len($args['nozero'])>=args['numofzero']:
	!@ если длина строки без нулей больше числа разрядов, или равна ему
	$args['end']=$args['nozero']	& !@ дробная часть приводится к строке без нулей
else
	!@ если число разрядов больше строки без нулей
	$args['end']=$mid($args['end'],1,args['numofzero'])
end
!@ убираем лишние нули в целой части
$args['start']=$strfind($args['start'],'(0*)(\d*)$',2)
if $args['start']='': $args['start']='0'	& !@ если в целой части были только нули, оставляем один ноль
! @ если дробная часть не отсеклась, возвращаем число с точкой, иначе - без
if $args['end']<>'':
	$result=$args['+']+$args['start']+'.'+$args['end']
elseif $args['start']<>0:
	$result=$args['+']+$args['start']
else
	$result='0'
end
--- #dz# ---------------------------------
!@pp:endif

Функция не использует иных функций библиотеки
