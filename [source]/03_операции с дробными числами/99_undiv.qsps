QSP-Game Переводит рациональное число в целое. (фактически умножает рациональное число на круглое число, с округлением-отсечением дробной части)
$args[0] - рациональное число
args[1] - точность перевода 10 - один знак после запятой, 100 - два знака, и т.д, по умолчанию до последнего знака после запятой
result - целое число
!@pp:if(em_undiv):include
# #undiv#
$args[9]=$args[9]	& !@ инициализация аргументов
!@ приводим args[1] к круглому значению
if args[1]<>0:
	!@ ------------ этот отрезок кода можно заменить на функцию em_raz -------------
		if args[1]<0: args[1]=-args[1]	& !@ модуль числа для удобства вычислений
		args['10']=1	& !@ то самое основание
		:for
		if no (args[1]/args['10']<10):
			args['10']=args['10']*10
			jump 'for'
		end
	!@ ------------ этот отрезок кода можно заменить на функцию em_raz ------------
end
if isnum($args[0])<>0 and args[1]<>0:
	!@ если строка не содержит точку и args[1] задан, просто умножаем число на args[1]
	result=val($args[0])*args[1]
	exit
elseif isnum($args[0])<>0 and args[1]=0:
	!@ если args[1] не задан и точки в строке нет, возвращаем само число
	result=val($args[0])
else
	!@ получаем только нули из args[1] в виде строки
	$args['0']=$mid($str(args[1]),2)
	!@ если вернулась пустая строка, просто удаляем точку из числа
	if $args['0']='': result=val($replace($args[0],'.')) & exit
	args['dot']=instr($args[0],'.')	& !@ получаем позицию точки в строке
	$args['end']=$mid($args[0],args['dot']+1)	& !@ получаем часть после точки
	$args['start']=$mid($args[0],1,args['dot']-1)	& !@ получаем часть до точки
	if len($args['end'])>=len($args['0']):
		!@ если длина конечной части числа равна, или превышает длину строки нулей
		!@ к начальной части добавляем часть конечной, равной длине строки нулей
		result=val($args['start']+$mid($args['end'],1,len($args['0'])))
	else
		!@ если длина конечной части меньше длины нулей, начальную и конечную часть склеиваем и добавляем несколько нудей
		result=val($args['start']+$args['end']+$mid($args['0'],1,len($args['0'])-len($args['end'])))
	end
end
--- #undiv# ---------------------------------
!@pp:endif

Функция не использует иных функций библиотеки
