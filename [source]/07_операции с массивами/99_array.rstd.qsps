QSP-Game Процедура перетасовывает элементы массива согласно сведениям в массиве фиксации.
$args[0] - массив, данные в котором необходимо перетасовать (исследуемый);
$args[1] - массив фиксации, где находится список, согласно которому тасуются элементы (опорный);
$args[2] - массив результатов - сюда помещается итог перетасовки, если не указан результат помещается в исходный массив.
!@pp:if(em_array_rstd):include
# #array.rstd#
$args[9]=$args[9]	& !@ инициализация аргументов
if instr($args[1],'$')=1: $args[1]=$mid($args[1],2)	& !@ опорный массив не может быть текстовым
if $args[2]='': $args[2]=$args[0]	& !@ если не указан результирующий массив, результат помещается в исходный
if instr($args[0],'$')=1: $args['$']='$' else $args['$']=''	& !@ маркер указания на тип
killvar 'easy_math_sort_array'	& !@ удаляем временный массив (защита от дурака)
killvar $args['$']+'easy_math_copy_array'	& !@ удаляем временный массив (защита от дурака)
copyarr 'easy_math_sort_array',$args[1]	& !@ копируем опорный массив во временный
!@ с помощью цикла вставляем элементы во временный массив
args['i']=0
:for
if args['i']<arrsize('easy_math_sort_array'):
	dynamic $args['$']+"easy_math_copy_array[args[0]] = "+$args[0]+"[args[1]]",args['i'],easy_math_sort_array[args['i']]
	args['i']+=1
	jump 'for'
end
copyarr $args[2],$args['$']+"easy_math_copy_array"	& !@ копируем данные из временного массива в результирующий
killvar $args['$']+'easy_math_copy_array'	& !@ удаляем временный массив
killvar 'easy_math_sort_array'	& !@ удаляем временный массив
--- #array.rstd# ---------------------------------
!@pp:endif

Функция не использует иных функций библиотеки