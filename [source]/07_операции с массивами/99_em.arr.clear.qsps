QSP-Game Функция для удаления из массива всех элементов с указанным значением.

!@ $0 - название массива (для строк и кортежей префикс типа обязателен).
!@ $1 - значение, которое хотим удалить из массива (тип должен совпадать с типом массива)
!@pp:if(em_arr_clear):include
# em.arr.clear
local em_arr_clear_copy_array_
copyarr 'em_arr_clear_copy_array_', $args[0]
loop local pos, size = 0, arrsize('em_arr_clear_copy_array_') while 1:
	!@ в зависимости от того, название текстового или числового массива передано, ищем текстовое или числовое значение
	if instr($args[0],'%')=1:
		pos = arrpos('%em_arr_clear_copy_array_', %args[1], pos)
	elseif instr($args[0],'$')=1:
		!@ поиск среди текстовых значений массива
		pos = arrpos('$em_arr_clear_copy_array_', $args[1], pos)
	else
		!@ поиск среди числовых значений массива
		pos = arrpos('em_arr_clear_copy_array_', args[1], pos)
	end
	!@ если элемент найден, размер массива больше нуля, и счётчик не исчерпан
	if pos <> -1 and arrsize('em_arr_clear_copy_array_') > 0 and size > 0:
		killvar 'em_arr_clear_copy_array_', pos	& !@ удаляем элемент
		size -= 1	& !@ уменьшаем значение счётчика
	else:
		jump 'break'
	end
end
:break
copyarr $args[0],'em_arr_clear_copy_array_'
--- em.arr.clear ---------------------------------
!@pp:endif

Функция не использует иных функций библиотеки

i[0]=1
i[1]=2
i[2]=3
i[3]=1
i[4]=2
i[5]=3
i[6]=1
i[7]=2

@em.arr.clear('i',2)
@em.arr.print('i')
