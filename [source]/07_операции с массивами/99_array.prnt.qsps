QSP-Game возвращает содержимое массива
Аргументы см. ниже
!@pp:if(em_array_prnt):include
# #array.prnt#
$args[9] = $args[9]	& !@ инициализация аргументов
$args[1] = $lcase($args[1])	& !@ переводим команды в нижний регистр
if $args[2]='': $args[2]='
'	& !@ если $args[2] не указано, в качестве разделителя использует символ переноса строки
if $args[2]='/se': $args[2]=''	& !@ если указан ключ,разделитель не используется
if args[3]<0: args[3]=0	& !@ защита от дурака
if args[4]<args[3]: args[4]=0	& !@ защита от дурака
!@ если указано, что нужно обрезать пробелы у значений, в dyneval будет добавляться команда trim
if instr($args[1],'[trim]')<>0: $args['trim.open']='TRIM(' & $args['trim.close']=')'
!@ если не выводим только результаты, содержимое оборачивается в кавычки
if instr($args[1],'[only result]')=0:
	$args['quotes']="'"
	$args['only_result']=''
else:
	$args['quotes']=""
	$args['only_result']='yep'
end
:for
if args[3]<arrsize($args[0]):
	!@ args[3] - счётчик. Пока счётчик не достигнет размера массива.
	!@ если не указано, что нужно вывести только значения массивов, к выводимым значениям добавляется название массива и номер ячейки
	if $args['only_result']='': $args['array.prnt']+=$args[0]+"[<<args[3]>>]="
	!@ получаем значение с помощью $dyneval и добавляем к результату
	$args['array.prnt']+=$args['quotes']+$dyneval("$result = $replace(<<$args['trim.open']>><<$args[0]>>[args[0]]<<$args['trim.close']>>,$args[1],""''"")",args[3],$args['quotes'])+$args['quotes']+$args[2]
	args[3]+=1	& !@ увеличиваем значение счётчика
	if args[3]<=args[4] or args[4]=0: jump 'for'	& !@ пока счётчик не превысит args[4] либо args[4]=0, потворяем
end
!@ если последний разделитель следует исключить, вырезаем
if instr($args[1],'[not last]')<>0: 
	$args['array.prnt']=$mid($args['array.prnt'],1,len($args['array.prnt'])-len($args[2]))
end
$result=$args['array.prnt']	& !@ возвращаем результат
--- #array.prnt# ---------------------------------
!@pp:endif

Функция не использует иных функций библиотеки

$args[0]- имя массива, содержимое которого необходимо просмотреть.
$args[1] - управляющая конструкция. Можно использовать три команды в любых комбинациях:
	* [only result] - выводит только содержимое массива, не показывая его названия и номера ячеек.
	* [trim] - отсекает прилегающие пробелы и символы табуляции при выводе значения элемента массива.
	* [not last] - исключает добавление разделителя за последним выводимым значением.
$args[2] - этим параметром задаётся разделитель между выводимыми значениями. По умолчанию, т.е. когда параметр не указан или равен пустому значению, между выводимыми значениями вставляется символ перевода строки. Ключ /se, указанный вместо разделителя, отключает вставку разделителя.
 args[3] - начало вывода. Номер элемента, с которого начинается вывод значений (включительно).
 args[4] - окончание вывода. Номер элемента, до которого следует продолжать вывод значений (включительно).
 