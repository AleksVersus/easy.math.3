QSP-Game Упрощённое заполнение массива
$args[0]- название массива, элементы которого необходимо заполнить
$args[1] - кортеж значений для заполнения массива
$args[2] - режим:
	* Не указано - в массив передаются как текстовые так и числовые значения,
	* [only number] или [num] - весь массив формируется, как числовой
	* [only string] или [str] - весь массив формируется, как строковый
args[3] - с какого элемента начинать заполнение. По умолчанию с 0
!@pp:if(em_arr_fill):include
# em.arr.fill
$args[1] = $args[1]+""
$args[2] = $lcase($args[2])
if $args[2]='[only string]': $args[2] = '[str]'
if $args[2]='[only number]': $args[2] = '[num]'
if instr($args[0],'$')=1: $args[0]=$mid($args[0],2) & !@ убираем из имени массива символ $
local instring, $cut & !@ объявляем локальные переменные
loop while len($args[1])>0:
    !@ циклом перебираем строку значений (кортеж)
    instring=instr($args[1],"")
    $cut=$mid($args[1],1,instring-1)
    $args[1]=$mid($args[1],instring+1)
    if (isnum($cut) and $args[2]='') or $args[2]='[num]':
    	dynamic "<<$args[0]>>[<<args[3]>>]=args[0]",val($cut)
    elseif (no isnum($cut) and $args[2]='') or $args[2]='[str]':
    	dynamic "$<<$args[0]>>[<<args[3]>>]=$args[0]",$cut
    end
    args[3]+=1
end
--- em.arr.fill ---------------------------------
!@pp:endif

Функция не использует иных функций библиотеки

Внимание!!! Функция не создаёт новый массив, а заполняет массив значениями!

@em.arr.fill('mass',(123,234,345))

loop local i=0 while i<arrsize('mass') step i+=1:
    *pl mass[i]
end

Примечание: чтобы понимать, каким образом мы вытаскиваем данные из кортежа, нужно понимать, в каком виде в QSP представлен кортеж. Кортеж — это строка значений. Фактически это обычная строка, в которой все значения разделяются символом "``". Этот символ имеет код 0x1f. Именно эту особенность хранения данных мы и используем, чтобы извлечь значения из кортежа. Мы определяем, в какой позиции находится разделяющий символ, затем вырезаем строку до этого символа и помещаем в новую ячейку массива. Дальше работаем уже с оставшейся строкой.
