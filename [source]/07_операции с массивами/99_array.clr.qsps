QSP-Game Функция для удаления из массива всех элементов с указанным значением.
$args[0] - название массива.
$args[1] - значение, которое хотим удалить из массива.
!@pp:if(em_array_clr):include
# #array.clr#
$args[9] = $args[9]	& !@ инициализация аргументов
args['size']=arrsize($args[0])	& !@ получаем размер массива и выставляем счётчик
:for
!@ в зависимости от того, название текстового или числового массива передано, ищем текстовое или числовое значение
if $AVS_PLAYERTYPE='[QN]':
	!@ это вариант для Quest Navigator
	if instr($args[0],'$')=1:
		!@ поиск среди текстовых значений массива
		args['pos']=arrpos($args[0],$args[1],args['pos'])
	else
		!@ поиск среди числовых значений массива
		args['pos']=arrpos($args[0],args[1],args['pos'])
	end
else
	!@ вариант для классического плеера
	if instr($args[0],'$')=1:
		args['pos']=arrpos(args['pos'],$args[0],$args[1])
	else
		args['pos']=arrpos(args['pos'],$args[0],args[1])
	end
end
!@ если элемент найден, размер массива больше нуля, и счётчик не исчерпан
if args['pos']<>-1 and arrsize($args[0])>0 and args['size']>0:
	killvar $args[0],args['pos']	& !@ удаляем элемент
	args['size']-=1	& !@ уменьшаем значение счётчика
	jump 'for'
end
--- #array.clr# ---------------------------------
!@pp:endif

Функция не использует иных функций библиотеки