QSP-Game Вставка элемента в массив со сдвигом вправо.
$args[0] - название массива.
$args[1] - значение, которое хотим присвоить элементу.
$args[2] - индекс элемента, куда хотим поместить новое значение, можно строковый.
!@pp:if(em_array_ins):include
# #array.ins#
$args[9] = $args[9]	& !@ инициализация аргументов
if instr($args[0],'$')=1:
	!@ если имеем дело с текстовым массивом
	$args[3]='$'	& !@ выставляем переменной-маркеру значение '$' (используется как префик названию массива)
	$args[5]="'<<$args[1]>>'"	& !@ для dyneval и dynamic значение из аргумента замыкаем в апострофы
else
	!@ если имеем дело с числовым массивом
	$args[3]=''	& !@ переменная-маркер с пустым значением
	$args[5]=$str(args[1])	& !@ для dyneval и dynamic значение из аргумента просто приводится к строке
end
!@ если передан строковый индекс:
if $args[2]<>'':
	if $args[3]='$':
		!@ для текстового массива в качестве замещающего значения используем следующую строку:
		$args[4]="'щЭcmыz9!T7K1АЙ@>FуОk7aСeV0mвзZхaAVSMARKUPDOWNY4З9ЪaХМDXqKA6ЛTЯк2цГРYцЙОЮX_6jmпКЫЖUxuАmlюVщцтщЭcmыz9!T7K1АЙ@>FуОk7aСeV0mвзZхaAVSMARKUPDOWNY4З9ЪaХМDXqKA6ЛTЯк2цГРYцЙОЮX_6jmпКЫЖUxuАmlюVщцт'"
	else
		!@ для числового массива выбираем наименее вероятное число в качестве замещающего значения:
		$args[4]='-2147483648'
	end
	args[2]=dyneval("
!@pp:запоминаем значение элемента
				<<$args[3]>>args[0]=<<$args[0]>>['<<$args[2]>>']
!@pp:в сам элемент помещаем замещающее значение
				<<$args[0]>>['<<$args[2]>>']=<<$args[4]>>
!@pp:возвращаем позицию элемента (т.к. замещающее значение не повторяется, других таких элементов быть не может)
				result=arrpos('<<$args[0]>>',<<$args[4]>>)
!@pp:и вновь записываем в элемент исходное значение
				<<$args[0]>>[result]=<<$args[3]>>args[0]
			")
end
copyarr "<<$args[3]>>easy_math_copy_array_1",$args[0],0,args[2]	& !@ копируем во временный массив первую часть исходного массива
copyarr "<<$args[3]>>easy_math_copy_array_2",$args[0],args[2]	& !@ копируем во второй временный массив вторую часть исх. массива
copyarr $args[0],"<<$args[3]>>easy_math_copy_array_1"	& !@ копируем обратно в исходный массив первую часть из временного массива
dynamic "
!@pp:вставляем элемент в конец массива
	<<$args[0]>>[]=<<$args[5]>>
!@pp:циклом переносим из временного массива вторую часть массива в исходный
	:for
	if arrsize('<<$args[3]>>easy_math_copy_array_2')>0:
		<<$args[0]>>[]=<<$args[3]>>easy_math_copy_array_2[0]
		killvar '<<$args[3]>>easy_math_copy_array_2',0
		jump 'for'
	end
"
!@ удаляем временные массивы
killvar '<<$args[3]>>easy_math_copy_array_1'
killvar '<<$args[3]>>easy_math_copy_array_2'
--- #array.ins# ---------------------------------
!@pp:endif

Функция не использует иных функций библиотеки, однако поскольку используется цикл, функция довольно медленная. Для сто тысячного массива вставка элемента ближе к началу массива занимает около 8 секунд.

$args[3] - перменная-маркер, указывающая тип массива. '$' - означает, что массив строковый, иначе - числовой.
$args[4] - в этой переменной запоминаем замещающее значение
$args[5] - приведённое к строке значение, которое хотим вставить в массив