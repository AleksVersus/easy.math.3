QSP-Game Заполняет массив порядковыми числами
$args[0]- название массива, элементы которого необходимо сформировать (исследуемый массив), обязательный параметр
args[1] - Сколько элементов необходимо заполнить, обязательный параметр
args[2] - верхняя граница чисел. До какого числа вести заполнение.
args[3] - нижняя граница чисел. С какого числа начинать заполнение.
args[4] - с какого элемента начинать заполнение.
!@pp:if(em_arr_strtfill):include
# em.arr.strtFill
args[9] = args[9]	& !@ инициализация аргументов и локальной переменной
if args[2]<args[3]:
	!@ если верхняя и нижняя границы чисел перепутаны,
	$args['reverse']='yes'	& !@ включается режим заполнения массива в обратном порядке
end
args['b']=args[3]	& !@ выставляем первое вносимое в массив число
if instr($args[0],'$')=1: $args['q']="'"	& !@ если массив текстовый, вносим текстовые значения
args[1]+=args[4]	& !@ по args[1] определяем верхнюю границу номера элемента (не включительно)
loop while args[4]<args[1] step args[4]+=1:
	!@ args[4] выступает счётчиком, и пока счётчик не достигнет args[1] присваиваем значения:
	dynamic "<<$args[0]>>[<<args[4]>>]=<<$args['q']>><<args['b']>><<$args['q']>>"
	!@ в зависимости от направления заполнения, увеличиваем или уменьшаем значение
	if $args['reverse']<>'yes':
		args['b']+=1
		if args['b']>args[2] and (args[2]<>0 or args[3]<>0): args['b']=args[3]
	else
		args['b']-=1
		if args['b']<args[2] and (args[2]<>0 or args[3]<>0): args['b']=args[3]
	end
end
--- em.arr.strtFill ---------------------------------
!@pp:endif

Функция не использует иных функций библиотеки

Внимание!!! Функция не создаёт новый массив, а заполняет массив значениями!

@em.arr.strtFill('t',8)

loop local i,size=0,arrsize('t') while i<size step i+=1:
	*pl "t[<<i>>]=<<t[i]>>"
end
*pl
@em.arr.strtFill('b',8,4)

loop local i,size=0,arrsize('b') while i<size step i+=1:
	*pl "b[<<i>>]=<<b[i]>>"
end
*pl
@em.arr.strtFill('z',8,0,2)

loop local i,size=0,arrsize('z') while i<size step i+=1:
	*pl "z[<<i>>]=<<z[i]>>"
end
*pl
@em.arr.strtFill('f',8,5,3,2)

loop local i,size=0,arrsize('f') while i<size step i+=1:
	*pl "f[<<i>>]=<<f[i]>>"
end
