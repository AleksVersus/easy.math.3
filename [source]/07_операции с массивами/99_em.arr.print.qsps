QSP-Game возвращает содержимое массива
Аргументы см. ниже
!@pp:if(em_arr_print):include
# em.arr.print
local em_arr_print_copy_array_	& !@ объявляем локальный массив для хранения выводимых значений
copyarr 'em_arr_print_copy_array_', $args[0]	& !@ копируем значения в локальный массив
!@ инициализируем локальные переменные
local $type_, $quotes_, $only_result_, $array_print_, $inline_
local $array_name_, $commands_, $delimiter_, start_cell_, end_cell_ = $args[0], $args[1], $args[2], args[3], args[4]
$commands_ = $lcase($commands_)	& !@ переводим команды в нижний регистр
if $delimiter_ = '': $delimiter_ = '
'	& !@ если $delimiter_ не указано, в качестве разделителя использует символ переноса строки
if $delimiter_ = '/se': $delimiter_ = ''	& !@ если указан ключ,разделитель не используется
if start_cell_ < 0: start_cell_ = 0	& !@ защита от дурака
if end_cell_ < start_cell_: end_cell_ = 0	& !@ защита от дурака
!@ если название массива начинается с $, мы имеем дело с текстовым массивом
if instr($array_name_, '$') = 1: $type_ = 'str' else $type_ = 'int'
!@ если не выводим только результаты, содержимое оборачивается в кавычки
if instr($commands_, '[only result]') = 0:
	$quotes_ = "'"
	$only_result_ = ''
else
	$quotes_ = ""
	$only_result_ = 'yep'
end
loop local i, j, size_ = start_cell_, end_cell_, arrsize('em_arr_print_copy_array_') while (i<size_ and (i<=j or j=0)) step i+=1:
	!@ если не указано, что нужно вывести только значения массивов, к выводимым значениям добавляется название массива и номер ячейки
	if $only_result_ = '': $array_print_ += $array_name_ + "[<<i>>]="
	if $type_ = 'str':
		!@ если пытаемся получить строковые значения, это может быть строка или кортеж
		if instr($em_arr_print_copy_array_[i], '') = 0:
			$inline_ = $replace($em_arr_print_copy_array_[i], $quotes_, "''")
			if instr($commands_, '[trim]') <> 0: $inline_ = $trim($inline_)
			$array_print_ += $quotes_ + $inline_ + $quotes_ + $delimiter_
		else
			$array_print_ += "(" + $replace($em_arr_print_copy_array_[i], '', ',') + ')' + $delimiter_
		end
	elseif $type_ = 'int':
		!@ если пытаемся получить число, просто вписываем его
		$array_print_ += $str(em_arr_print_copy_array_[i]) + $delimiter_
	end
end
!@ если последний разделитель следует исключить, вырезаем
if instr($commands_, '[not last]') <> 0: 
	$array_print_ = $mid($array_print_, 1, len($array_print_) - len($delimiter_))
end
$result = $array_print_	& !@ возвращаем результат
--- em.arr.print ---------------------------------
!@pp:endif

Функция не использует иных функций библиотеки

$args[0]- имя массива, содержимое которого необходимо просмотреть.
$args[1] - управляющая конструкция. Можно использовать три команды в любых комбинациях:
	* [only result] - выводит только содержимое массива, не показывая его названия и номера ячеек.
	* [trim] - отсекает прилегающие пробелы и символы табуляции при выводе значения элемента массива.
	* [not last] - исключает добавление разделителя за последним выводимым значением.
$args[2] - этим параметром задаётся разделитель между выводимыми значениями. По умолчанию, т.е. когда параметр не указан или равен пустому значению, между выводимыми значениями вставляется символ перевода строки. Ключ /se, указанный вместо разделителя, отключает вставку разделителя.
args[3] - начало вывода. Номер элемента, с которого начинается вывод значений (включительно).
args[4] - окончание вывода. Номер элемента, до которого следует продолжать вывод значений (включительно).

$a[]='нулевая ячейка'
$a[]='   первая ячейка   '
$a[]=' вторая ячейка'
$a[]=''
$a[]=(1,2,3,5,6)
$a[]='   пятая ячейка'
$a[]=''
$a[]='седьмая ячейка'

@em.arr.print('$a')
@em.arr.print('$a','[only result]')
@em.arr.print('$a','[trim]')
@em.arr.print('$a','[trim] [only result]','|',2,5)
@em.arr.print('$a','[trim] [only result] [not last]','|',2,5)

