QSP-Game Функция, конвертирующая десятеричное число в шестнадцатеричное

!@ #0 - десятеричное число
!@ $r - шестнадцатеричное число
!@pp:if(em_dec_in_hex):include
# em.dec.in_hex
local $minus_, base_16_, num_of_digit_
$result=''
if args[0]<0: args[0]=-args[0] & $minus_='-'	& !@ поддержка отрицательных чисел
!@ генерируем последнее основание и номер последнего разряда:
base_16_=1
num_of_digit_=0
loop while no (args[0]/base_16_<16) step num_of_digit_+=1:
	base_16_*=16
end
loop local run=1 while run:
	if num_of_digit_=0:
		$result+=$mid('0123456789abcdef',args[0]+1,1)	& !@ получаем символ числа (hex-цифру)
		run=0
	else
		$result+=$mid('0123456789abcdef',args[0]/base_16_+1,1)	& !@ получаем hex-цифру
		args[0]=args[0] mod base_16_	& !@ числом становится остаток от деления на основание
		num_of_digit_-=1
		!@ --- получение нового основания. Можно заменить на функцию #^# (em.EXP)
			base_16_=1
			loop local i=num_of_digit_ while i>0 step i-=1:
				base_16_*=16
			end
		!@ ------------------------- получение нового основания -----------------
	end
end
$result=$minus_+$result
--- em.dec.in_hex ---------------------------------
!@pp:endif

Функция не использует иных функций библиотеки.

@em.dec.in_hex(2147483647)	& !@ '7fffffff'
@em.dec.in_hex(214)	& !@ 'd6'
@em.dec.in_hex(255)	& !@ 'ff'
@em.dec.in_hex(10)	& !@ 'a'
@em.dec.in_hex(9)	& !@ '9'
@em.dec.in_hex(2147483648)	& !@ '7fffffff'
