QSP-Game Функция, конвертирующая десятеричное число в шестнадцатеричное (см ниже)

!@ #0 - число целое, десятеричное в пределах от -255 до 255
!@ $r - шестнадцатеричное двуразрядное число (текстовое значение).
!@pp:if(em_dec_in_col):include
# em.dec.in_col
local $minus_, base_16_, num_of_digit_
$result=''
if args[0]<0: args[0]=-args[0] & $minus_='-'
if args[0]>255: args[0]=255
!@ генерируем последнее основание и номер последнего разряда:
!@ можно заменить на em.LOG
base_16_=1
num_of_digit_=0
loop while no (args[0]/base_16_<16):
	base_16_*=16
	num_of_digit_+=1
end
loop local run=1 while run:
!@ если само число меньше 16
	if num_of_digit_=0:
		$result+=$mid('0123456789abcdef',args[0]+1,1)	& !@ получаем символ числа (hex-цифру)
		run=0
	else
		$result+=$mid('0123456789abcdef',args[0]/base_16_+1,1)	& !@ получаем hex-цифру
		args[0]=args[0] mod base_16_	& !@ числом становится остаток от деления на основание
		num_of_digit_-=1
		!@ --- получение нового основания. Можно заменить на функцию #^# (em.EXP)
			base_16_=1
			loop local i=num_of_digit_ while i>0 step i-=1:
				base_16_*=16
			end
		!@ ------------------------- получение нового основания -----------------
	end
end
if len($result)=1:
	$result='0'+$result
elseif $result='':
	$result='00'
end
$result=$minus_+$result
--- em.dec.in_col ---------------------------------
!@pp:endif

Отличием от функции em.dec.in_hex является ограничение на значение аргумента в пределах [-255 ... 255]. Если пределы превышены, функция выдаёт в результат ff. Любое число в пределах преобразуется к двуразрядному шестнадцатеричному числу. Таким образом ограничение на результат лежит в пределах [-ff ... ff].
В общем и целом, такое преобразование необходимо для того, чтобы силами QSP конвертировать значения цветовой составляющей от HTML формата (где запись идёт в шестнадцатеричном коде) в RGB формат (где запись идёт в десятеричном коде). Используется в библиотеке easy.titles.qsp для ковертирования цветовых значений.

!@pp:Рекомендуется не использовать эту функцию, а использовать функцию dec-hex или надстройки над ней.

@em.dec.in_col(2147483647)	& !@ 'ff'
@em.dec.in_col(214)	& !@ 'd6'
@em.dec.in_col(255)	& !@ 'ff'
@em.dec.in_col(-10)	& !@ '-0a'
@em.dec.in_col(256)	& !@ 'ff'
@em.dec.in_col(16)	& !@ '10'
