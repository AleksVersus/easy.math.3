QSP-Game Функция складывает два шестнадцатеричных числа

!@ $0 - первое слагаемое /обязательно текстовое/
!@ $1 - второе слагаемое /обязательно текстовое/
!@pp:if(em_hex_add):include
# em.hex.add
!@ инициализация локальных переменных и приведение слагаемых к единой длине
local $first_add_, $second_add_ = $lcase($args[0]), $lcase($args[1])	& !@ инициализируем слагаемые
local max_len_, number_, $value_
number_['0']=0 & $value_[0]='0'
number_['1']=1 & $value_[1]='1'
number_['2']=2 & $value_[2]='2'
number_['3']=3 & $value_[3]='3'
number_['4']=4 & $value_[4]='4'
number_['5']=5 & $value_[5]='5'
number_['6']=6 & $value_[6]='6'
number_['7']=7 & $value_[7]='7'
number_['8']=8 & $value_[8]='8'
number_['9']=9 & $value_[9]='9'
number_['a']=10 & $value_[10]='a'
number_['b']=11 & $value_[11]='b'
number_['c']=12 & $value_[12]='c'
number_['d']=13 & $value_[13]='d'
number_['e']=14 & $value_[14]='e'
number_['f']=15 & $value_[15]='f'
if len($first_add_)>len($second_add_): max_len_=len($first_add_) else max_len_=len($second_add_)
local $em_zero_ = {	loop while args[0]>0 step args[0]-=1: $result+='0' } & !@ callback instead of using a function
$first_add_ = $dyneval($em_zero_, max_len_-len($first_add_)) + $first_add_
$second_add_ = $dyneval($em_zero_ ,max_len_-len($second_add_)) + $second_add_
loop local last_byte_,run=0,1 while run:
	$result=''
	last_byte_=0
	loop local i,res_byte_=max_len_,0 while i>0 step i-=1:
		!@ цикл выполняем, пока счётчик не достигнет нуля
		!@ складываем две цифры на разряде
		res_byte_=number_[$mid($first_add_,i,1)]+number_[$mid($second_add_,i,1)]+last_byte_
		!@ вычисляем переполнение
		last_byte_=res_byte_/16
		!@ вычисляем полученную цифру на текущем разряде
		res_byte_=res_byte_ mod 16
		!@ записываем полученную цифру в результат
		$result=$value_[res_byte_]+$result
	end
	if last_byte_>0:
		!@ если после всех итерраций осталось число в переполнении
		!@ Это число ставим самым верхним разрядом
		$result=$value_[last_byte_]+$result	
	end
	run=0	& !@ прерываем внешний цикл
end
--- em.hex.add ---------------------------------
!@pp:endif

Функция не использует иных функций библиотеки
