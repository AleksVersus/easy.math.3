QSP-Game Функция, конвертирующая шестнадцатеричное число в десятеричное

!@ $0 - шестнадцатеричное число
!@ #r - десятеричное число
!@pp:if(em_hex_in_dec):include
# em.hex.in_dec
$args[0] = $lcase($args[0])	& !@ переводим в нижний регистр все символы hex-числа (строка)
local $last_symbol_
result = 0
loop local i, t, len_ = 0, 0, len($args[0]) while i < len_ step i += 1:
	!@ пока счётчик меньше длины строки
	$last_symbol_ = $mid($args[0], len($args[0])-i, 1) & !@ вырезаем из строки указанную hex-цифру
	t = instr('0123456789abcdef', $last_symbol_) - 1 & !@ определяем значение цифры по таблице сопоставления
	!@ умножаем на 16 столько раз, каков номер разряда у текущей цифры:
	loop local j = i while j > 0 step j -= 1: t *= 16
	result += t	& !@ прибавляем к результату полученное число
end
--- em.hex.in_dec ---------------------------------
!@pp:endif

@em.hex.in_dec('ff')	& !@ 255
@em.hex.in_dec('22')	& !@ 34
@em.hex.in_dec('10')	& !@ 16
@em.hex.in_dec('1')	& !@ 1
@em.hex.in_dec('a')	& !@ 10
@em.hex.in_dec('ffaa233')	& !@ 268083763
@em.hex.in_dec('ffaa23')	& !@ 16755235
@em.hex.in_dec('ffaa2')	& !@ 1047202
@em.hex.in_dec('ffaa233451')	& !@ -1440533423
@em.hex.in_dec('ffaa23345')	& !@ -90033339
@em.hex.in_dec('ffaa2334')	& !@ -5627084
