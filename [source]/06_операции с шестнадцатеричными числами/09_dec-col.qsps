QSP-Game Функция, конвертирующая десятеричное число в шестнадцатеричное (см ниже)
args[0] - число целое, десятеричное в пределах от -255 до 255
$result - шестнадцатеричное двуразрядное число (текстовое значение).
!@pp:if(em_dec_col):include
# #dec-col#
$args[9]=$args[9]
$result=''
if args[0]<0: args[0]=-args[0] & $args['+']='-'
if args[0]>255: args[0]=255
!@ генерируем последнее основание и номер последнего разряда:
!@ можно заменить на em.LOG
args['base']=1
args['j']=0
:last_base
if no (args[0]/args['base']<16):
	args['base']=args['base']*16
	args['j']+=1
	jump 'last_base'
end
:new_base
!@ если само число меньше 16
if args['j']=0:
	$result+=$mid('0123456789abcdef',args[0]+1,1)	& !@ получаем символ числа (hex-цифру)
	jump 'end_keep'
else
	args['hn']=args[0]/args['base']
	$result+=$mid('0123456789abcdef',args['hn']+1,1)	& !@ получаем hex-цифру
	args[0]=args[0] mod args['base']	& !@ числом становится остаток от деления на основание
	args['j']-=1
	!@ --- получение нового основания. Можно заменить на функцию #^# (em.EXP)
		args['base']=1
		args['i']=args['j']
		:for
		if args['i']>0: 
			args['base']=args['base']*16
			args['i']-=1
			jump 'for'
		end
	!@ ------------------------- получение нового основания -----------------
	jump 'new_base'
end
:end_keep
if len($result)=1:
	$result='0'+$result
elseif $result='':
	$result='00'
end
$result=$args['+']+$result
--- #dec-col# ---------------------------------
!@pp:endif

Функция не использует иных функций библиотеки.

Отличием от функции #dec-hex# является ограничение на значение аргумента в пределах [-255 ... 255]. Если пределы превышены, функция выдаёт в результат ff. Любое число в пределах преобразуется к двуразрядному шестнадцатеричному числу. Таким образом ограничение на результат лежит в пределах [-ff ... ff].
В общем и целом, такое преобразование необходимо для того, чтобы силами QSP конвертировать значения цветовой составляющей от HTML формата (где запись идёт в шестнадцатеричном коде) в RGB формат (где запись идёт в десятеричном коде). Используется в библиотеке easy.titles.qsp для ковертирования цветовых значений.

!@pp:Рекомендуется не использовать эту функцию, а использовать функцию dec-hex или надстройки над ней.