QSP-Game Суммирует все элементы массива
$args[0] - название массива. (Если начинается с $ - склеиваются все текстовые значения массива)
$args[1] - разделитель или ключ /se. Если указан ключ - без разделителя.
!@pp:if(em_summ):include
# em.summ
local $_, $res
if $args[0]='': exit	& !@ если не указано имя массива. Защита от дурака
if $args[1]='':
	!@ если аргумент не указан, в качестве разделителя используется пробел
	$args[1]=' '
elseif $args[1]='/se':
	!@ если аргументом указан ключ, разделитель не используется
	$args[1]=''
end
!@ если указан текстовый массив, помечаем в переменной-маркере
if instr($args[0],'$')=1: $_='$'
loop local i,size=0,arrsize($args[0]) while i<size step i+=1:
	if $_='$':
		!@ для текстовых массивов раюотает конкатенация
		$res+=$dyneval("$result=<<$args[0]>>[<<i>>]")+$args[1]
	else
		!@ для числовых суммирование
		res+=dyneval("result=<<$args[0]>>[<<i>>]")
	end
end
!@ возвращение результата
if $_='$':
	$result=$res
else
	result=res
end
--- em.summ ---------------------------------
!@pp:endif
Функция не использует иных функций библиотеки

mass[]=12
mass[]=99
mass[]=123
@em.summ('mass')