QSP-Game Суммирует все элементы массива
$args[0] - название массива. (Если начинается с $ - склеиваются все текстовые значения массива)
$args[1] - разделитель или ключ /se. Если указан ключ - без разделителя.
!@pp:if(em_summ):include
# #sum#
$args[9]=$args[9]	& !@ инициализация аргументов, чтобы избежать ошибок
if $args[0]='': exit	& !@ если не указано имя массива. Защита от дурака
if $args[1]='':
	!@ если аргумент не указан, в качестве разделителя используется пробел
	$args[1] = ' '
elseif $args[1]='/se':
	!@ если аргументом указан ключ, разделитель не используется
	$args[1]=''
end
if instr($args[0],'$')=1:
	!@ если указан текстовый массив, помечаем в переменной-маркере
	$args['$']='$'
end
:for
if args['i']<arrsize($args[0]):
	if $args['$']='$':
		!@ для текстовых массивов раюотает конкатенация
		$args['result']+=$dyneval("$result=<<$args[0]>>[<<args['i']>>]")+$args[1]
	else
		!@ для числовых суммирование
		args['result']+=dyneval("result=<<$args[0]>>[<<args['i']>>]")
	end
	args['i'] += 1
	jump 'for'
end
!@ возвращение результата
if $args['$']='$':
	$result=$args['result']
else
	result=args['result']
end
--- #sum# ---------------------------------
!@pp:endif
Функция не использует иных функций библиотеки