QSP-Game Локация предназначена для быстрого теста библиотеки. Монтируется безусловно.

# em.usercom
!@ ----------------- switch on-off of input-string -----------------
	if $lcase($args[0]) = 'on':
		$usercom['em.usercom'] = 'em.usercom'
		exit
	elseif $lcase($args[0]) = 'off':
		killvar '$usercom', 'em.usercom'
		exit
	end
!@ ----------------- switch on-off of input-string -----------------

if $args[0] = '' and instr($user_text, 'em ') = 1:
	local $user_text_ = $mid($user_text, 4)
	local $em_test_parse_
	local $cmdline_ = $lcase($trim($user_text_))

	!@ ----------------- parse the words from input-line -----------------
		scanstr '$em_test_parse_', $cmdline_, '[^\s]+'
	!@ ----------------- parse the words from input-line -----------------

	if $cmdline_ = 'help':
		*pl & *pl 'ОСНОВНЫЕ КОМАНДЫ:'
		*pl & *pl "* logs — выводит содержимое массива '$log'. В этот массив можно сохранять сообщения при отладке модуля."
		*pl & *pl "* sqrt(N) — выводит результат вычисления квадратного корня из числа N."
		*pl & *pl "* gen id — генерирует уникальный идентификатор типа UUID четвёртой версии."
		*pl & *pl "* gen pass — генерирует парольную фразу из шестнадцати случайных символов (буквы латинского алфавита, цифры, !@#$)."
		*pl & *pl "* gen mass <ARRAY_NAME> <NUMBER_of_ELEMENTS> — генерирует массив с именем <ARRAY_NAME> из <NUMBER_of_ELEMENTS> элементов."
		*pl & *pl "* autotest — запускает на выполнение автотесты библиотеки для проверки её работоспособности. Внимание!!! Автотесты нужны для отладки самой библиотеки, или для проверки корректности её функций перед окончательной сборкой. Из окончательной сборки автотесты можно удалить."
		*pl "Для теста отдельной функции, можно указать её название как аргумент команде autotest. Например: `autotest em.str.thin`."
		*pl & *pl "Любая иная строчка будет воспринята, как код QSP на выполнение. Таким образом вы можете выполнить любую строчку кода QSP, просто введя её в строке ввода."
	elseif $cmdline_='logs':
		if loc('em.arr.print'):
			*pl @em.arr.print('$log','[only result]')
		else:
			msg "Error. Function 'em.arr.print' is not exist! Print log by cycle:"
			loop local i=0 while i<arrsize('$log') step i+=1:
				*pl $log[i]
			end
		end
	elseif instr($cmdline_,'sqrt')=1:
		if loc('em.sqrt'):
			killvar '$em_test_parse_'
			$em_test_parse_[0]=$mid($cmdline_,instr($cmdline_,'(')+1,instr($cmdline_,')')-instr($cmdline_,'(')-1)
			if isnum($em_test_parse_[0])=0: $em_test_parse_[0]=$dyneval("$result=$str(<<$em_test_parse_[0]>>)")
			*pl @em.sqrt($em_test_parse_[0])
		else:
			msg "Error. Function 'em.sqrt' is not exist!"
		end
	elseif $cmdline_='gen id':
		if loc('em.gen_uuid'):
			*pl @em.gen_uuid()
		else:
			msg "Error. Function 'em.gen_uuid' is not exist!"
		end
	elseif $cmdline_='gen pass':
		if loc('em.str.random'):
			*pl @em.str.random(16,'[inc][\z][\d]','!@#$')
		else:
			msg "Error. Function 'em.str.random' is not exist!"
		end
	elseif instr($trim($lcase($cmdline_)),'gen mass')=1:
		if loc('em.arr.fill_rand') and loc('em.arr.print'):
			local count
			if $em_test_parse_[2]<>'':
				if $em_test_parse_[3]<>'' and isnum($em_test_parse_[3]): count=val($em_test_parse_[3]) else count=10
				@em.arr.fill_rand($em_test_parse_[2],count)
				@em.arr.print($em_test_parse_[2])
			else
				*pl "Error input. Type:"
				*pl "gen mass <ARRAY_NAME> <NUMBER_of_ELEMENTS>"
			end
		else:
			msg "Error. Functions 'em.arr.fill_rand' or 'em.arr.print' are not exist!"
		end
	elseif instr($cmdline_,'autotest') = 1 and loc('em.autotest.self'):
		@em.autotest.self($em_test_parse_[1], $em_test_parse_[2])
	else:
		dynamic $cmdline_
	end
end
--- em.usercom ---------------------------------
